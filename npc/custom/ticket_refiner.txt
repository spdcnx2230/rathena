//===== rAthena Script ======================================= 
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
morocc,128,81,5	script	Refine Master	429,{
	disable_items;
	if (countitem(6238) || countitem(6228) || countitem(6229) || countitem(6230) || countitem(6231) || countitem(6456))
		set .@bWeaponUp,1;
	if (countitem(6239) || countitem(6232) || countitem(6233) || countitem(6234) || countitem(6235) || countitem(6457))
		set .@bArmorUp,1;
	if (!.@bWeaponUp && !.@bArmorUp) {
		mes "[Refine Master]";
		mes "มีอะไรที่เจ้าไม่กล้า ตี+ไหม?";
		mes "ถ้ามีตั๋ว(Ticket)";
		mes "ข้าจะทำให้มันเป็นจริง";
		next;
		switch(select("อย่ามากวนดิ๊.:อะไร..ยังไง...หะ?")) {
		case 1:
			mes "[Refine Master]";
			mes "ไป๊ปปปป.";
			close;
		case 2:
			mes "[Refine Master]";
			mes "อัพเกรดไปเทมโดยใช้ ^006400Refine Ticket^000000...";
			mes "แจ่มว๊าว~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[Refine Master]";
	mes "สวัสดีผู้เจริญแล้ว!";
	mes "ข้าสามารถอัพเกรดไอเทมตามระดับ ^006400ticket^000000.";
	mes "เจ้าไม่ต้องกลัวว่าไอเทมเข้าจะเสียหา สบายใจได้.";
	next;
	if(select("ไว้มาใหม่นะ.:อัพเรกดไอเทมโดยใช้ ticket.") == 1) {
		mes "[Refine Master]";
		mes "Okay.";
		mes "ไว้มาใหม่น้าาาาา.";
		close;
	}
	mes "[Refine Master]";
	mes "เจ้าจะให้ข้าอัพเกรดอะไรดีละ?";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@indices[.@i]))?getequipname(.@indices[.@i]):.@position$[.@i]+"- [Empty]")+":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[Refine Master]";
		mes "เจ้าต้องสวมใส่มันก่อนนะ.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[Refine Master]";
		mes "Oh, I'm sorry.";
		mes "This item is impossible to refine.";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		setarray .@tickets[0],6457,6235,6234,6233,6232,6239;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Armor";
		set .@check,.@bArmorUp;
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		setarray .@tickets[0],6456,6231,6230,6229,6228,6238;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Weapon";
		set .@check,.@bWeaponUp;
		break;
	}
	if (!.@check) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "ถ้าเจ้าต้องการอัพเกรด ^006400"+.@type$+"^000000, เจ้าต้องมี ^006400"+.@type$+" Ticket^000000.";
		mes "ไปหามาดิ๊!";
		close;
	}
	mes "[Refine Master]";
	mes "เลือก ^006400"+.@type$+" Refine Ticket^000000 ที่เจ้าต้องการจะใช้.";
	next;
	set .@menu$,"";
	for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1)
		set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
	set .@select, select(.@menu$)-1;
	set .@ticket_lv, .@levels[.@select];
	set .@ticket_id, .@tickets[.@select];
	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[Refine Master]";
		mes getitemname(.@ticket_id)+" เจ้าไม่มีมันนิ !!?";
		mes "หาดูอีกทีดิ๊.";
		mes "ไป๊!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Refine Master]";
		mes "^8B4513ไอเทมได้รับการอัพเกรดแล้ว.^000000";
		mes "ไว้มาใช้บริการใหม่น๊าาาาา.";
		close;
	}
	mes "[Refine Master]";
	mes "ข้าจะทำการอัพเกรด ^006400"+getequipname(.@part)+"^8B4513 จนถึง +"+.@ticket_lv+" ระดับ^000000 ของ ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "ให้ข้าลุยเลยม่ะ?";
	next;
	if(select("ไม่เอา.:ลุยเลยพรี่.") == 1) {
		emotion ET_THINK;
		mes "[Refine Master]";
		mes "โห่.";
		mes "ได้ดิ.";
		mes "ไปไหนก็ไปสะ";
		close;
	}
	mes "[Refine Master]";
	mes "แจ่มว๊าว.";
	mes "ได้เสมอ!";
	mes "พร้อมจะลุยกันมานานแล้ว...";
	mes ".....ฮาๆโย่ว!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id)) {
		delitem .@ticket_id,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) ||
			callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
			mes "[Refine Master]";
			emotion ET_FRET;
			mes "รอแปบ..";
			mes "คิดว่าข้าโง่รึ?!";
			mes "ข้าเห็นเจ้าสลับของนะ !";
			close;
		}
	} else {
		next;
		mes "Error!";
		mes "โดนจดชื่อไว้ละ.";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[Refine Master]";
	mes "แจ่มว๊าว, นี้มัน~";
	mes "เยี่ยม, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "ยินดีด้วยนะ "+.@type$+".";
	mes "ลาก๊อนนนน~!";
	close;

OnInit:
waitingroom "ตีบวกใช้ Ticket",0;
end;	

}

