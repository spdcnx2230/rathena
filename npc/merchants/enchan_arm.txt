//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

morocc,123,82,2	script	Apprentice Craftsman	73,{
	disable_items;
	mes "[Apprentice Craftsman]";
	mes "ต้องมีขั้นต่ำ250,000 zeny";
	if (Zeny >= 250000) {
		mes "ข้าสามารถเสริมพลังให้กับ ชุดเกราะของเจ้าได้.";
		next;
		mes "[Apprentice Craftsman]";
		mes "มีโอกาสได้รับ";
		mes "Status +1~3";
		mes "Fighting Spirit1 1~3";
		mes "Spell 1~3";
		mes "Sharp 1~3";
		mes "เพิ่มผลการฟื้นฟู";
		mes "MHP และ MSP";
		next;
		mes "[Apprentice Craftsman]";
		mes "ในต้วเจ้าจะต้องมี ^5555ffชุดเกราะ^000000 แค่ชิ้นเดียวเท่านั้น";
		next;
		mes "[Apprentice Craftsman]";
		mes "^FF0000อาจมีข้อผิดพลาดหากในตัวเจ้ามีของชิ้นเดียวกันมากกว่า 1 ชิ้น^FF0000";
		next;
		switch(select("ชุดเกราะ ไม่มีรู.:ชุดเกราะ มีรู:High Grade Armor.:ลาก่อน.")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2355,2340,2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],15015,15016,15017,15018,15019,15020,15021,2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[Apprentice Craftsman]";
			mes "กลับมาใหม่ละถ้าสนใจ enchanting.";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "ถ้าเงินแค่นี้ไม่มีก้ ลาก่อน.";
	next;
	mes "[Apprentice Craftsman]";
	mes "enchant armor มีค่าใช้จ่าย 250,000 zeny.";
	close;

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[Apprentice Craftsman]";
	if (countitem(.@itemid) == 1) {
		mes "ค่าใช้จ่ายอยู่ที่ 250,000 zeny. ราคาเร้าๆ.";
		next;
		mes "[Apprentice Craftsman]";
		mes "^FF0000คำเตือน!!^FF0000";
		mes "^ff5555ระดับการอัพเกรดของชุด";
		mes "และ Cards จะหายไป!!.^000000";
		mes "ยังต้องการจะ Enchant? อยู่ไหม";
		next;
		if(select("ข้าไม่อยากแล้ว.:ลุยเลย.") == 1) {
			mes "[Apprentice Craftsman]";
			mes "คนที่กลัวการสูญเสีย มักไม่เจริญ!!";
			mes "แค่ระดับอัพเกรดหาย+การ์ดหายแค่นี้ถึงกับป๊อด...";
			close;
		}
		mes "[Apprentice Craftsman]";
		mes "โฮ่ๆๆ งั้นเราก็ลุยกันเลย?";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 250000) {
			mes "[Apprentice Craftsman]";
			mes "เจ้ามี zeny. ไม่พอ";
			close;
		}
		progressbar "ffff00",3;
		emotion ET_BEST;
		set Zeny, Zeny-250000;
		delitem .@itemid,1;
		switch (rand(1,36)) {
			case 1: set .@addpart,4702;break;	//Strength3
			case 2: set .@addpart,4712;break;	//Inteligence3
			case 3: set .@addpart,4722;break;	//Dexterity3
			case 4: set .@addpart,4732;break;	//Agility3
			case 5: set .@addpart,4742;break;	//Vitality3
			case 6: set .@addpart,4752;break;	//Luck3
			case 7: set .@addpart,4701;break;	//Strength2
			case 8: set .@addpart,4711;break;	//Inteligence2
			case 9: set .@addpart,4721;break;	//Dexterity2
			case 10: set .@addpart,4731;break;	//Agility2
			case 11: set .@addpart,4741;break;	//Vitality2
			case 12: set .@addpart,4751;break;	//Luck2
			case 13: set .@addpart,4700;break;	//Strength1
			case 14: set .@addpart,4710;break;	//Inteligence1
			case 15: set .@addpart,4720;break;	//Dexterity1
			case 16: set .@addpart,4730;break;	//Agility1
			case 17: set .@addpart,4740;break;	//Vitality1
			case 18: set .@addpart,4750;break;	//Luck1
			case 19: set .@addpart,4811;break;	//Fighting_Spirit1
			case 20: set .@addpart,4810;break;	//Fighting_Spirit2
			case 21: set .@addpart,4809;break;	//Fighting_Spirit3
			case 22: set .@addpart,4805;break;	//Heal_Amount2
			case 23: set .@addpart,4850;break;	//Heal_Amount3
			case 24: set .@addpart,4815;break;	//Spell1
			case 25: set .@addpart,4814;break;	//Spell2
			case 26: set .@addpart,4813;break;	//Spell3
			case 27: set .@addpart,4818;break;	//Sharp1
			case 28: set .@addpart,4817;break;	//Sharp2
			case 29: set .@addpart,4816;break;	//Sharp3
			case 30: set .@addpart,4797;break;	//MHP300
			case 31: set .@addpart,4798;break;	//MHP400
			case 32: set .@addpart,4799;break;	//MHP500
			case 33: set .@addpart,4800;break;	//MSP50
			case 34: set .@addpart,4801;break;	//MSP100
			case 35: set .@addpart,4805;break;	//MSP150
			case 36: set .@addpart,4832;break;	//Expert_Archer1
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[Apprentice Craftsman]";
				mes "แย่หน่อยนะ.";
				mes "^FF0000enchant พลาด^FF0000";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "เจ้าไม่มีของนิ!";
		mes "ไว้กลับมาใหม่ถ้ามีของละกัน.";
		close;
	}
}
